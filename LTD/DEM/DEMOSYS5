Følgende er eksempler på indskrivning af de forskellige former af lister:
.sp
Ordnet liste:
:ol.
:li.Dette er første punkt
:li.Og dette er andet punkt i den ordnede liste.
:eol.
.sp
Uordnet liste (som gøres compact, dvs. uden en blank linie imellem de
enkelte punkter - dette kan gøres ved alle former for listerne):
:ul compact.
:li.Og her er så første punkt i den uordnede liste
:li.Andet punkt.
:eul.
.sp
Simpel liste:
:sl.
:li.Første punkt af den simple liste
:li.Og andet punkt!
:esl.
.sp
Og så en definitionsliste (opbygget med KOMMANDO-PARRENE DT og DD - disse
skal altid følges ad - dvs. INGEN andre kommandoer imellem).  Desuden
findes der nogle argumenter til bl.a. at fortælle hvor langt inde,
den forklarende tekst skal starte, og om den venstrestillede tekst skal
være med alm. skrift, fremhævet eller understreget (se pkt. 2 i forrige
skærmbillede - her er vist, hvorledes denne tekst er indskrevet):
:dl tsize=17 termhi=3.
:dt.Første pkt.
:dd.Og her følger så den forklarende tekst til første punkt.
:dt.Andet pkt.
:dd.Og forklarende tekst til andet punkt.  Her skal lige skrives lidt
mere tekst til dette punkt, så det kan ses hvor pænt, den højrestillede
tekst står i forhold til den venstrestillede.
:edl.
.sp 2
De enkelte former for lister kan også flettes ind i hinanden, se
nedenstående eksempel:
:dl termhi=1 tsize=10.
:dt.Første
:dd.Forklarende tekst.  Nu skal der flettes en compact ordnet liste
ind i denne definitionsliste:
:ol compact.
:li.Første punkt af den compacte liste
:li.Og her følger andet punkt.
:li.Tredie punkt.  Nu skal den ordnede liste afsluttes, så vi kan
komme videre med definitionslisten.
:eol.
:dt.Andet
:dd.Her er så andet punkt af denne definitionsliste, som indeholder en
compact, ordnet liste.  Nu afsluttes denne definitionsliste med kommandoen
EDL.
:edl.
